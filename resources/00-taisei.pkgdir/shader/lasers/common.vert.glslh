
#include "../lib/render_context.glslh"
#include "../lib/util.glslh"
#include "../interface/standard.glslh"

UNIFORM(1) vec2 origin;
UNIFORM(2) float timeshift;
UNIFORM(3) float width;
UNIFORM(4) float width_exponent;
UNIFORM(5) int span;
UNIFORM(6) vec2 args[4];

vec2 pos_rule(float t);

#include "vertex_pos.glslh"

void main(void) {
	float instance = gl_InstanceID;
	float t = instance * 0.5 + timeshift;

	vec2 p = pos_rule(t);
	vec2 d = p - pos_rule(t - 0.1);

	float tail = span / 1.6;
	float mid_ofs = instance - span * 0.5;
	float s = -1 / (tail * tail) * (mid_ofs - tail) * (mid_ofs + tail);
	s = 0.75 * pow(s, width_exponent);

	vec2 pos     = laser_vertex_pos(p, d, s);
	gl_Position  = r_projectionMatrix * r_modelViewMatrix * vec4(pos, 0.0, 1.0);
	texCoord     = (r_textureMatrix * vec4(texCoordRawIn, 0.0, 1.0)).xy;
	texCoordRaw  = texCoordRawIn;
}
